<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="Custom.SWARH.Report.RptSvc.StoredProc.ProgramDetails">
<Description>
***************************************************************************************************************************************
* Report Code: Custom.SWARH.Report.RptSvc.StoredProc.ProgramDetails
* Report Description: Datasets to be used by Reporting Server reports requiring specific Community Program derived data
* Menu Caption: N/A
* Initial Release: 21/07/2016 Developer: Michael Welsh
* Modified:
***************************************************************************************************************************************</Description>
<ProcedureBlock>0</ProcedureBlock>
<Super>Custom.SWARH.Report.ZEN.StoredProc.Abstract</Super>
<TimeCreated>60905,54849</TimeCreated>

<Parameter name="SrcVer">
<Description>
Location and Revision of this file in Perforce (Auto-updating)</Description>
<Default>$Id: //custom_ccrs/au/AUSW/T2016/BASE/cls/Custom/SWARH/Report/RptSvc/StoredProc/ProgramDetails.xml#1 $</Default>
</Parameter>

<Method name="ProgramsCSVExecute">
<Description>
This is the Execute section for the %Library.Query. 
All of the logic will be performed here.
A new node for the ^CacheTemp global will be created and each subscript of this global will contain
a row for the report.</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&QHandle:%Library.Binary,ProgramsCSV:%String]]></FormalSpec>
<ReturnType>%Library.Status</ReturnType>
<Implementation><![CDATA[
 // Get reportid i.e. use $INCREMENT to add another node to ^CacheTemp global. 
 // We use ^CacheTemp global because it will always use memory before disk
 New repid,ind

 // Use $Increment to get the next node
 Set repid=$INCREMENT(^CacheTemp(..#CACHETEMPSUBSCRIPT))
 
 New ProgramsString

 Set (ProgramsString)=""
 
 Set ind=1, END=0

 //Start Monitor (if configured to capture stats)
 Set mon=..MonitorBegin()
 
 &SQL(DECLARE ProgramsCSVCursor CURSOR FOR
 	SELECT
 		LIST(DEP_Desc) AS ProgramsString
	FROM
      	SQLUser.CT_NFMI_CategDepart
	WHERE
      	INSTR(',' || :ProgramsCSV || ',', ',' || DEP_RowID || ',') <> 0
	)
	 	
 &SQL(OPEN ProgramsCSVCursor)
 
 //FETCH FIRST ROW
 Do FetchRowProgramsCSV
  
 While END=0 {
  
  	 Do FormatStringsProgramsCSV
  	 
	 Set sort = ind	
 
	 Set ^CacheTemp(..#CACHETEMPSUBSCRIPT,repid,sort)=$LISTBUILD(ProgramsString)
	 Set ind=ind+1
 
	 //fetch rows
	 Do FetchRowProgramsCSV
 }

 &SQL(CLOSE ProgramsCSVCursor)
 
 // Build QHandle (AtEnd,ReportID,Index)
 Set QHandle=$ListBuild(0,repid,0)

 Quit $$$OK

FetchRowProgramsCSV
 &SQL(FETCH ProgramsCSVCursor INTO :ProgramsString)
 If SQLCODE '= 0 Set END = 1
 Quit
 
FormatStringsProgramsCSV
 if ProgramsString'="" s ProgramsString=$REPLACE(ProgramsString,",",", ")
 Quit
]]></Implementation>
</Method>

<Method name="ProgramsCSVFetch">
<Description>
This is the Fetch component of the %Library.Query. 
This should never have to change. It is designed to fetch the row for each subscript of the ^CacheTemp
global and return this row to the Crystal Report.</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&QHandle:%Library.Binary,&Row:%Library.List,&AtEnd:%Library.Integer=0]]></FormalSpec>
<ReturnType>%Library.Status</ReturnType>
<Implementation><![CDATA[
 // This fetch method should never have to change. 
 // repid - Report ID
 // ind   - sequence index which represents each row

 New repid,ind

 // Restore QHandle
 Set AtEnd=$LIST(QHandle,1)
 Set repid=$LIST(QHandle,2)
 Set ind=$LIST(QHandle,3)

 Set ind=$ORDER(^CacheTemp(..#CACHETEMPSUBSCRIPT,repid,ind))
 If ind="" {	// if there are no more rows, finish fetching
	 Set AtEnd=1
	 Set Row=""
 }
 Else      {	// fetch row
 	Set Row=^CacheTemp(..#CACHETEMPSUBSCRIPT,repid,ind)
 }

 // Save QHandle
 Set QHandle=$LISTBUILD(AtEnd,repid,ind)
 Quit $$$OK
]]></Implementation>
</Method>

<Method name="ProgramsCSVClose">
<Description>
This is the Close component of the %Library.Query.
This is standard code and should never have to change.
This is where all the cleanup is performed, i.e. the purging of the temporary storage global ^CacheTemp.</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>QHandle:%Library.Binary</FormalSpec>
<ReturnType>%Library.Status</ReturnType>
<Implementation><![CDATA[
 // Clean up by purging the temporary node in ^CacheTemp global
 New repid,ind
 Set repid=$LIST(QHandle,2)

 Kill ^CacheTemp(..#CACHETEMPSUBSCRIPT,repid)
 Quit $$$OK
]]></Implementation>
</Method>

<Query name="ProgramsCSV">
<Type>%Library.Query</Type>
<FormalSpec>ProgramsCSV:%String</FormalSpec>
<SqlProc>1</SqlProc>
<Parameter name="CONTAINID" value="0"/>
<Parameter name="ROWSPEC" value="ProgramsString:%String"/>
<Parameter name="SELECTMODE" value="ODBC"/>
</Query>
</Class>
</Export>
