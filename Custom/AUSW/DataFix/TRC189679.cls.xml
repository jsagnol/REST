<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="Custom.AUSW.DataFix.TRC189679">
<ClassType/>
<ProcedureBlock>0</ProcedureBlock>
<Super>%RegisteredObject</Super>
<TimeCreated>63173,28078.665462</TimeCreated>

<Parameter name="SrcVer">
<Description>
Location and Revision of this file in Perforce (Auto-updating)</Description>
<Default>$Id: //custom_ccrs/au/AUSW/T2016/BASE/cls/Custom/AUSW/DataFix/TRC189679.xml#1 $</Default>
</Parameter>

<Method name="start">
<Description><![CDATA[
TRC189679 - Update ALM_DisplayAlert field and avoid table triggers

d ##class(Custom.AUSW.DataFix.TRC189679).start(<apply fix>,<backup>,<purge backup>,<restore>)
d ##class(Custom.AUSW.DataFix.TRC189679).start(1,1,1,0)]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>apply:%Boolean=0,backup:%Boolean=1,purge:%Boolean=0,restore:%Boolean=0</FormalSpec>
<Implementation><![CDATA[
	/*
	UPDATE PA_AlertMsg
	SET ALM_DisplayAlert = 'Y'
	WHERE ((ALM_DisplayAlert IS NULL) OR (ALM_DisplayAlert != 'Y'))
	AND ((ALM_ClosedFlag IS NULL) OR (ALM_ClosedFlag != 'Y'))
	AND ((ALM_ClosedDate > '2015-02-26') OR (ALM_ClosedDate IS NULL))
	*/
	w !,"Routine start: "_$zdt($h,3)
	
	s check="Y"
	i purge w !,"WARNING!!! - You are purging your backup.  Continue? (y/n) " r check
	
	i ##class(%Collation).AlphaUp($e(check))'="Y" {
		w !,"Update cancelled..."
		return
	} else {
		i purge {
			i $g(^zTMP("TRC189679"))'="" w !,"Last backup: "_$zdt(^zTMP("TRC189679"),3)
			w !,"Purging old backup..."
			k ^zTMP("TRC189679")
		}
	}
	
	s ind=0
	// If restore then set value back to previous from our backup
	i restore {
		i '$d(^zTMP("TRC189679","BACKUP")) {
			w !,"No backup exists..."
		} else {
			w !,"Restoring from ^zTMP(""TRC189679"",""BACKUP"")..."
			i $g(^zTMP("TRC189679"))'="" w !,"Last backup: "_$zdt(^zTMP("TRC189679"),3)
			
			s id=0
			f {
				s id=$o(^zTMP("TRC189679","BACKUP",id)) q:id=""
				s $p(^PAPER(+id,"ALM",$p(id,"||",2)),"^",22)=$g(^zTMP("TRC189679","BACKUP",id))
				s ind=$i(ind)
			}
			w !,"Number of records restored: "_ind
		}
	} else {
		i apply w !,"Updating database..."
		s pat=0
		f {
			s pat=$o(^PAPER(pat)) q:pat=""
			s alm=0
			f {
				s alm=$o(^PAPER(pat,"ALM",alm)) q:alm=""
				i $p($g(^PAPER(pat,"ALM",alm)),"^",22)'="Y",$p($g(^PAPER(pat,"ALM",alm)),"^",7)'="Y" {
					i (($p($g(^PAPER(pat,"ALM",alm)),"^",15)="")||($p($g(^PAPER(pat,"ALM",alm)),"^",15)>$zdh("2015-02-26",3))) {
						; Update ALM_DisplayAlert to "Y" - No index on this field so ok to set global.  This will also bypass table triggers which is required so no HL7 messages are generated
						
						i backup {
							i $g(^zTMP("TRC189679"))="" {
								s ^zTMP("TRC189679")=$h
								w !,"Backup existing values..."
							}
							s ^zTMP("TRC189679","BACKUP",pat_"||"_alm)=$p($g(^PAPER(pat,"ALM",alm)),"^",22)
						}
						s ind=$i(ind)
						i apply s $p(^PAPER(pat,"ALM",alm),"^",22)="Y"
					}
				}
			}
		}
		i apply {
			w !,"Number of records updated: "_ind
		} else {
			w !,"Number of records to be updated: "_ind
		}
	}
	w !,"Routine stop: "_$zdt($h,3)
	w !,"Done..."
]]></Implementation>
</Method>
</Class>
</Export>
